plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.graalvm.buildtools.native' version '0.10.4'
    id 'jacoco'
}

group = 'io.github.bodote'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.jspecify:jspecify:1.0.0'
    implementation 'software.amazon.awssdk:s3:2.25.67'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.htmlunit:htmlunit:3.11.0'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.4'
    testImplementation 'org.testcontainers:localstack:1.20.4'
    testImplementation 'com.microsoft.playwright:playwright:1.49.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    include '**/*IT.class'
    shouldRunAfter test
}

jacocoTestReport {
    dependsOn test, integrationTest
    executionData fileTree(layout.buildDirectory.dir('jacoco')) {
        include '*.exec'
    }
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.register('coverageAll') {
    group = 'verification'
    description = 'Runs all tests (including *IT) and generates JaCoCo coverage reports.'
    dependsOn test, integrationTest, jacocoTestReport
}

graalvmNative {
    binaries {
        main {
            imageName = 'woodle'
            buildArgs.add('--no-fallback')
        }
    }
}
